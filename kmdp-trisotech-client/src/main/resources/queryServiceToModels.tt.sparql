# PREFIX sets up the namespaces to be used in the query
PREFIX rdf:       <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graph:     <http://www.trisotech.com/graph/1.0/element#>
PREFIX graphrel:  <http://www.trisotech.com/graph/1.0/elementRel#>
PREFIX skos:      <http://www.w3.org/2004/02/skos/core#>
PREFIX owl:       <http://www.w3.org/2002/07/owl#>
PREFIX rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:       <http://www.w3.org/2001/XMLSchema#>
PREFIX dmn:       <http://www.trisotech.com/graph/1.0/decision/element#>
PREFIX bpmn:      <http://www.trisotech.com/graph/1.0/workflow/element#>
PREFIX assetType: <https://www.omg.org/spec/API4KP/20200801/taxonomy/KnowledgeAssetType#>

SELECT ?model ?version ?serviceId ?assetType ?artifactName ?serviceName ?serviceNode
FROM NAMED ?
WHERE {
GRAPH ? {
    {
    ?model rdf:type graph:Model .
    ?model rdfs:label ?artifactName .

    ?model graphrel:mimeType ?mimeType .
    FILTER (?mimeType NOT IN("application/bpmn-2-0+xml", "application/vnd.triso-capability+json","application/vnd.triso-landscaping+json","application/vnd.triso-discovery+json" ))

    ?serviceNode rdf:type dmn:DecisionService .
    # exclude decision services generated by default.
    # no other publicly accessible way to distinguish them from the user defined ones?
    FILTER ( ! STRENDS( STR(?serviceNode), "_DS") ) .

    ?serviceNode graphrel:owner* ?model .
    ?serviceNode rdfs:label ?serviceName .

    OPTIONAL {
        ?customAttribute2 a graph:CustomAttribute .
        ?customAttribute2 graphrel:key ?key2
        FILTER regex(?key2, "serviceAssetId", "i") .
        ?customAttribute2 graphrel:value ?serviceId .
        ?serviceNode graphrel:customAttribute ?customAttribute2 .
    }

    OPTIONAL { ?model graphrel:version ?version } .

    BIND ( assetType:15cac118-853d-32f1-b70a-3d99328eeda2 AS ?assetType ) .
    }
UNION
    {
    ?model rdf:type graph:Model .
    ?model rdfs:label ?artifactName .

    ?model graphrel:mimeType "application/bpmn-2-0+xml" .

    ?serviceNode rdf:type bpmn:Process .

    ?customAttribute3 a graph:CustomAttribute .
    ?customAttribute3 graphrel:key ?key3
    FILTER regex(?key3, "serviceAssetId", "i") .
    ?customAttribute3 graphrel:value ?serviceId .
    ?serviceNode graphrel:customAttribute ?customAttribute3 .

    ?serviceNode graphrel:owner* ?model .
    ?serviceNode rdfs:label ?serviceName .

    OPTIONAL { ?model graphrel:version ?version } .

    BIND ( assetType:15cac118-853d-32f1-b70a-3d99328eeda2 AS ?assetType ) .
   }
 }
}
GROUP BY ?model ?version ?serviceId ?assetType ?artifactName ?serviceName ?serviceNode
